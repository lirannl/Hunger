#Imports
 #import minetweaker.item.IItemStack;
 import mods.buildcraft.AssemblyTable;
 import mods.exnihilo.Crucible;
 import mods.bloodmagic.Altar;
 import mods.bloodmagic.Alchemy;
 import mods.nei.NEI;
#Vanilla
 val cobble = <ore:cobblestone>;
 val stone = <minecraft:stone>;
 val wood = <ore:logWood>;
 val sapling = <ore:treeSapling>;
 val coalOre = <ore:oreCoal>;
 val coal = <ore:coal>;
 val charCoal = <minecraft:coal:1>;
 val anyStone = <ore:stone>;
 val gunpowder = <minecraft:gunpowder>;
 val goldNugget = <minecraft:gold_nugget>;
 val wheat = <minecraft:wheat>;
 val potato = <minecraft:potato>;
 val carrot = <minecraft:carrot>;
 val brewingStand = <minecraft:brewing_stand>;
 val netherrack = <minecraft:netherrack>;
 val boneItem = <minecraft:bone>;
 val diamondBlock = <minecraft:diamond_block>;
 val sugarcane = <minecraft:reeds>;
 val stoneBricks = <minecraft:stonebrick>;
 val clay = <minecraft:clay_ball>;
 val clayBlock = <minecraft:clay>;
 val waterBucket = <minecraft:water_bucket>;
 val milkBucket = <minecraft:milk_bucket>;
 val tileFurnace = <minecraft:furnace>;
 val netherQuartz = <minecraft:quartz>;
 val obsidian = <minecraft:obsidian>;
 val craftingTable = <minecraft:crafting_table>;
 val lever = <minecraft:lever>;
 val mushroomStew = <minecraft:mushroom_stew>;
 val bakedPotato = <minecraft:baked_potato>;
 val goldCarrot = <minecraft:golden_carrot>;
 val iceBlock = <minecraft:ice>;
 val piston = <minecraft:piston>;
 val rawFish = <minecraft:fish>;
 val rawSalmon = <minecraft:fish:1>;
 val cookedFish = <minecraft:cooked_fished>;
 val cookedSalmon = <minecraft:cooked_fished:1>;
 val goldenApple = <minecraft:golden_apple:0>;
 val chest = <minecraft:chest>;
 val flint = <minecraft:flint>;
 val gold = <ore:ingotGold>;
 val flintAndSteel = <minecraft:flint_and_steel:*>;
 val woodPlank = <ore:plankWood>;
 val dandelion = <minecraft:yellow_flower>;
 val yellow = <minecraft:dye:11>;
 val stick = <ore:stickWood>;
 val apple = <minecraft:apple>;
 val wheatSeeds = <minecraft:wheat_seeds>;
 val cocoa = <minecraft:dye:3>;
 val bread = <minecraft:bread>;
 val redstone = <minecraft:redstone>;
 val redstoneBlock = <minecraft:redstone_block>;
 val bonemeal = <minecraft:dye:15>;
 val chicken = <minecraft:chicken>;
 val cookedChicken = <minecraft:cooked_chicken>;
 val beef = <minecraft:beef>;
 val rawPork = <minecraft:porkchop>;
 val cookedPork = <minecraft:cooked_porkchop>;
 val gravel = <minecraft:gravel>;
 val spawnCow = <minecraft:spawn_egg:92>;
 val spawnChicken = <minecraft:spawn_egg:93>;
 val rottenFlesh = <minecraft:rotten_flesh>;
 val leather = <minecraft:leather>;
 val sugar = <minecraft:sugar>;
 val steak = <minecraft:cooked_beef>;
 val blazePowder = <minecraft:blaze_powder>;
 val diamond = <ore:gemDiamond>;
 val iron = <ore:ingotIron>;
 val wool = <minecraft:wool>;
 val itemString = <minecraft:string>;
 val red = <ore:dyeRed>;
 val pink = <ore:dyePink>;
 val glowstone = <ore:dustGlowstone>;
 val glowstoneBlock = <ore:glowstone>;
 val witherSkull = <minecraft:skull:1>;

#AE2:
 val skyStone = <appliedenergistics2:tile.BlockSkyStone>;
 val certus = <appliedenergistics2:item.ItemMultiMaterial>;
 val chargedCertus = <appliedenergistics2:item.ItemMultiMaterial:1>;
 val cookedSkyStone = <appliedenergistics2:tile.BlockSkyStone:1>;
 val accelerationCard = <appliedenergistics2:item.ItemMultiMaterial:30>;
#Botania
 val livingRock = <Botania:livingrock>;
 val livingWood = <Botania:livingwood>;
 val manaSpreader = <Botania:spreader>;
 val manaSteelBlock = <Botania:storage>;
 val redPetal = <Botania:petal:14>;
 val pureDaisyOut = <Botania:specialFlower>.withTag({type: "puredaisy"});
#Blood Magic
 val bloodAltar = <AWWayofTime:Altar>;
 val sacrificialKnife = <AWWayofTime:sacrificialKnife>;
 val weakBloodOrb = <AWWayofTime:weakBloodOrb>;
 val bloodBrewing = <AWWayofTime:blockWritingTable>;
#Big Reactors
 val yellorium = <BigReactors:BRIngot>;
#Ex Nihilo
 val crook = <exnihilo:crook>;
 val boneCrook = <exnihilo:crook_bone>;
 val exBarrel = <exnihilo:barrel>;
 val crucible = <exnihilo:crucible>;
 val littleStone = <exnihilo:stone>;
 val copper = <ore:ingotCopper>;
 val weakGlowstone = <exnihilo:exnihilo.gold_powdered>;
#Extra Utilities
 val furnaceGen = <ExtraUtilities:generator:1>;
 val compressedCobble = <ExtraUtilities:cobblestone_compressed>;
 val tripleCompressedCobble = <ExtraUtilities:cobblestone_compressed:2>;
 val angelRing = <ExtraUtilities:angelRing:0>;
 val angelRingFeather = <ExtraUtilities:angelRing:1>;
 val angelRingFairy = <ExtraUtilities:angelRing:2>;
 val angelRingDragon = <ExtraUtilities:angelRing:3>;
 val angelRingGold = <ExtraUtilities:angelRing:4>;
#EnderIO
 val solarPanel = <EnderIO:blockSolarPanel>;
 val steelBall = <EnderIO:itemMaterial:7>;
#Forestry
 val honeyDrop = <ore:dropHoney>;
 val pregFrame = <Forestry:frameImpregnated>;
 val fluidMint = <liquid:ice>;
 val oilCapsule = <Forestry:waxCapsuleOil>;
#Mekanism/tools/generators
 val steel = <ore:ingotSteel>;
 val osmium = <ore:ingotOsmium>;
 val steelSword = <MekanismTools:SteelSword>;
 val metalInfuser = <Mekanism:MachineBlock:8>;
 val crusher = <Mekanism:MachineBlock:3>;
 val steelCasing = <Mekanism:BasicBlock:8>;
 val mekControlCircuit = <Mekanism:ControlCircuit>;
 val eliteECube = <Mekanism:EnergyCube:*>.onlyWithTag({tier: "Elite"});
 val sulfur = <ore:dustSulfur>;
 val eliteCircuit = <ore:circuitElite>;
 val refGlowstone = <Mekanism:Ingot:3>;
 val compressedCarbon = <Mekanism:CompressedCarbon>;
#MFR
 val rawMeatBlock = <MineFactoryReloaded:tile.mfr.decorativebrick:12>;
 val rawMeatNugget = <MineFactoryReloaded:item.mfr.meat.nugget.raw>;
 val meatBucket = <MineFactoryReloaded:item.mfr.bucket.meat>;
 val rawRubber = <MineFactoryReloaded:item.mfr.rubber.raw>;
 val rawMeatIngot = <MineFactoryReloaded:item.mfr.meat.ingot.raw>;
 val cookedMeatIngot = <MineFactoryReloaded:item.mfr.meat.ingot.cooked>;
 val chocolateMilk = <liquid:chocolatemilk>;
#Pam's Harvestcraft
 val presser = <harvestcraft:presser>;
 val salt = <harvestcraft:saltItem>;
 val appleSapling = <harvestcraft:pamappleSapling>;
 val pistachioSapling = <harvestcraft:pampistachioSapling>;
 val cashewSapling = <harvestcraft:pamcashewSapling>;
 val cinnamonSapling = <harvestcraft:pamcinnamonSapling>;
 val nut = <ore:listAllnut>;
 val walnut = <ore:cropWalnut>;
 val chestnut = <ore:cropChestnut>;
 val peanut = <ore:cropPeanut>;
 val vanilla = <harvestcraft:vanillaItem>;
 val pistachio = <ore:cropPistachio>;
 val cashew = <ore:cropCashew>;
 val pecan = <ore:cropPecan>;
 val almond = <ore:cropAlmond>;
 val extremeChili = <harvestcraft:extremechiliItem>;
 val vegetable = <ore:listAllveggie>;
 val lavenderBread = <harvestcraft:lavendershortbreadItem>;
 val fishTaco = <harvestcraft:fishtacoItem>;
 val HChoney = <harvestcraft:honeyItem>;
 val tomato = <ore:cropTomato>;
 val lettuce = <ore:cropLettuce>;
 val onion = <ore:cropOnion>;
 val date = <harvestcraft:dateItem>;
 val lemon = <harvestcraft:lemonItem>;
 val garlic = <ore:cropGarlic>;
 val rice = <harvestcraft:riceItem>;
 val barley = <harvestcraft:barleyItem>;
 val rye = <harvestcraft:ryeItem>;
 val oats = <harvestcraft:oatsItem>;
 val peppermint = <harvestcraft:peppermintItem>;
 val chili = <harvestcraft:chilipepperItem>;
 val rawMutton = <harvestcraft:muttonrawItem>;
 val cookedMutton = <harvestcraft:muttoncookedItem>;
 val cookedChestnut = <harvestcraft:roastedchestnutItem>;
 val cookingOil = <harvestcraft:oliveoilItem>;
 val hotdog = <harvestcraft:hotdogItem>;
 val heatBalls = <harvestcraft:strawberrysaladItem>;
 val zeppole = <harvestcraft:zeppoleItem>;
 val jellybeans = <harvestcraft:jellybeansItem>;
 val caramel = <harvestcraft:caramelItem>;
 val yogurt = <harvestcraft:plainyogurtItem>;
 val iceCream = <harvestcraft:icecreamItem>;
 val mintChocChipIceCream = <harvestcraft:mintchocolatechipicemcreamItem>;
 val epicBacon = <harvestcraft:epicbaconItem>;
 val supremePizza = <harvestcraft:supremepizzaItem>;
 val pizza = <harvestcraft:pizzaItem>;
 val pecanPie = <harvestcraft:pecanpieItem>;
 val heartyBreakfast = <harvestcraft:heartybreakfastItem>;
 val hamburger = <harvestcraft:hamburgerItem>;
 val baconMushroomBurger = <harvestcraft:baconmushroomburgerItem>;
 val cheeseBurger = <harvestcraft:cheeseburgerItem>; 
 val baconCheeseBurger = <harvestcraft:baconcheeseburgerItem>;
 val deluxeCheeseBurger = <harvestcraft:deluxecheeseburgerItem>;
 val delightedMeal = <harvestcraft:delightedmealItem>;
 val chips = <harvestcraft:friesItem>;
 val sushi = <harvestcraft:sushiItem>;
 val redVelvetCake = <harvestcraft:redvelvetcakeItem>;
 val pasta = <harvestcraft:pastaItem>;
 val spagetti = <harvestcraft:spagettiItem>;
 val spagettiAndMeat = <harvestcraft:spagettiandmeatballsItem>;
 val wellington = <harvestcraft:beefwellingtonItem>;
 val rainbow = <harvestcraft:rainbowcurryItem>;
 val vanillaIceCream = <harvestcraft:vanillaicecreamItem>;
 val sausageInBread = <harvestcraft:sausageinbreadItem>;
 val mapleBacon = <harvestcraft:maplecandiedbaconItem>;
 val candiedLemon = <harvestcraft:candiedlemonItem>;
 val flour = <harvestcraft:flourItem>;
 val cheese = <harvestcraft:cheeseItem>;
 val baconDates = <harvestcraft:baconwrappeddatesItem>;
 val fruitSalad = <harvestcraft:fruitsaladItem>;
 val marzipan = <harvestcraft:lemonbarItem>;
 val teaLeaf = <harvestcraft:tealeafItem>;
 val mortarAndPestle = <harvestcraft:mortarandpestleItem>;
 val saucepan = <harvestcraft:saucepanItem>;
 val skillet = <harvestcraft:skilletItem>;
 val bakeware = <harvestcraft:bakewareItem>;
 val pot = <harvestcraft:potItem>;
 val persimmonSandwich = <harvestcraft:persimmonjellysandwichItem>;
 val donut = <harvestcraft:donutItem>;
 val toastSandwich = <harvestcraft:toastsandwichItem>;
 val peanutButterCookies = <harvestcraft:peanutbuttercookiesItem>;
 val brownie = <harvestcraft:brownieItem>;
 val manjuu = <harvestcraft:manjuuItem>;
 val chocolateBar = <harvestcraft:chocolatebarItem>;
 val mint = <harvestcraft:seaweedItem>;
 val mintSeed = <harvestcraft:seaweedseedItem>;
 val mintBalls = <harvestcraft:picklesItem>;
 val caramelIceCream = <harvestcraft:caramelicecreamItem>;
 val dough = <harvestcraft:doughItem>;
 val taffy = <harvestcraft:taffyItem>;
 val vanillaCrop = <harvestcraft:vanillaItem>;
 val frostedDonut = <harvestcraft:frosteddonutItem>;
 val cocoaPowder = <harvestcraft:cocoapowderItem>;
 val butter = <harvestcraft:butterItem>;
 val chiliChoc = <harvestcraft:chilichocolateItem>;
 val frostyMint = <harvestcraft:limejellyItem>;
 val chilliPopper = <harvestcraft:chilipoppersItem>;
 val peanutButter = <harvestcraft:peanutbutterItem>;
 val hotWing = <harvestcraft:hotwingsItem>;
 val freshMilk = <harvestcraft:freshmilkItem>;
 val grilledCheese = <harvestcraft:grilledcheeseItem>;
 val mapleWaffles = <harvestcraft:maplesyrupwafflesItem>;
 val cotton = <harvestcraft:cottonItem>;
 val cottonSeeds = <harvestcraft:cottonseedItem>;
#Power Suits 
 val tinkerTable = <powersuits:tile.tinkerTable>;
 val MPShelm = <powersuits:item.powerArmorHelmet>;
 val MPSchest = <powersuits:item.powerArmorChestplate>;
 val MPSpants = <powersuits:item.powerArmorLeggings>;
 val MPSboots = <powersuits:item.powerArmorBoots>;
 val MPSfist = <powersuits:item.powerFist>;
 val MPSwiring = <powersuits:item.null>;
 val MPSsolenoid = <powersuits:item.null:1>;
 val MPSservo = <powersuits:item.null:2>;
 val MPSwing = <powersuits:item.null:3>;
 val MPSthruster = <powersuits:item.null:4>;
 val MPSlv = <powersuits:item.null:5>;
 val MPSmv = <powersuits:item.null:6>;
 val MPShv = <powersuits:item.null:7>;
 val MPSparachute = <powersuits:item.null:8>;
 val MPSironPlating = <powersuits:item.null:9>;
 val MPSdiamondPlating = <powersuits:item.null:10>;
 val MPSffEmitter = <powersuits:item.null:11>;
 val MPSholoEmitter = <powersuits:item.null:12>;
 val MPSmyofiber = <powersuits:item.null:13>;
 val MPScircuit = <powersuits:item.null:14>;
 val MPSgel = <powersuits:item.null:15>;
 val MPSmuscle = <powersuits:item.null:16>;
 val MPSsolar = <powersuits:item.null:17>;
 val MPSmagnet = <powersuits:item.null:18>;
 val MPSchip = <powersuits:item.null:19>;
#ProjectRed
 val redIllumar = <ProjRed|Core:projectred.core.part:33>;
 val greenIllumar = <ProjRed|Core:projectred.core.part:32>;
 val blueIllumar = <ProjRed|Core:projectred.core.part:30>;
#Thermal Expansion
 val hardGlass = <ore:blockGlassHardened>;
 val pyrotheum = <ThermalFoundation:material:512>;
 val enderium = <ore:ingotEnderium>;
 val coalDust = <ore:dustCoal>;
 val enderiumBlock = <ore:blockEnderium>;
 val fullTesseFrame = <ThermalExpansion:Frame:8>;
#Tinker's construct
 val aluminium = <ore:ingotAluminum>;
 val monsterJerky = <TConstruct:jerky:5>;
 val bloodDrop = <TConstruct:strangeFood:1>;
 val pigPlate = <TConstruct:heavyPlate:18>;
 val fairySecret = <ExtraTiC:fullGuard:142>;
 val candySecret = <TConstruct:fullGuard:15>;
 val rareBacon = <TConstruct:strangeFood:2>;
 val pigIngot = <TConstruct:materials:34>;
 val fancyBone = <TConstruct:decoration.multibrickfancy:9>;
 val searedBrick = <TConstruct:materials:2>;
#Custom Items
 val bloodenCobbleIn = <EnderIO:blockIngotStorage:3>.onlyWithTag({display: {Lore: ["Stone, imbued", "with blood"]}});
 val bloodenCobbleOut = <EnderIO:blockIngotStorage:3>.withTag({display: {Lore: ["Stone, imbued", "with blood"]}});
#Coloured Buckets
 val bucketRed = <AWWayofTime:bucketLife>;
 val bucketOrnage = <ThermalFoundation:bucket:3>;
 val bucketYellow = <ThermalFoundation:bucket:1>;
 val bucketGreen = <OpenBlocks:filledbucket>;
 val bucketBlue = <ThermalFoundation:bucket:4>;
 val bucketIndigo = <MineFactoryReloaded:item.mfr.bucket.biofuel>;
 val bucketPurple = <TConstruct:buckets:9>;
#Recipe removal
 furnace.remove(coal);
 furnace.remove(charCoal); 
 furnace.remove(cookedFish);
 furnace.remove(cookedSalmon);
 furnace.remove(cookedChicken);
 furnace.remove(steak);
 furnace.remove(cookedPork);
 furnace.remove(cookedMutton);
 furnace.remove(bread);
 furnace.remove(<harvestcraft:calamaricookedItem>);
 furnace.remove(<exnihilo:silkworm_cooked>);
 furnace.remove(<MineFactoryReloaded:item.mfr.meat.nugget.cooked>);
 furnace.remove(cookedMeatIngot);
 mods.exnihilo.Sieve.removeRecipe(<minecraft:coal>);
 mods.exnihilo.Sieve.removeRecipe(bonemeal);
 mods.exnihilo.Sieve.removeRecipe(<minecraft:glowstone_dust>);
 recipes.remove(furnaceGen);
 recipes.remove(coal);
 recipes.remove(tileFurnace);
 recipes.remove(boneCrook);
 recipes.remove(woodPlank);
 recipes.remove(chest);
 recipes.remove(goldCarrot);
 recipes.remove(mushroomStew);
 recipes.remove(sugar);
 recipes.remove(wheatSeeds);
 recipes.remove(steelSword);
 recipes.remove(manaSpreader);
 recipes.remove(bonemeal);
 recipes.remove(brewingStand);
 recipes.remove(bread);
 recipes.remove(heatBalls);
 recipes.remove(manjuu);
 recipes.remove(cheese);
 recipes.remove(baconDates);
 recipes.remove(frostedDonut);
 recipes.remove(metalInfuser);
 recipes.remove(crusher);
 recipes.remove(rawMeatBlock);
 recipes.remove(rawMeatIngot);
 recipes.remove(salt);
 recipes.remove(cookingOil);
 recipes.remove(sushi);
 recipes.remove(butter);
 recipes.remove(delightedMeal);
 recipes.remove(heartyBreakfast);
 recipes.remove(hotWing);
 recipes.remove(jellybeans);
 recipes.remove(brownie);
 recipes.remove(mintBalls);
 recipes.remove(epicBacon);
 recipes.remove(hamburger);
 recipes.remove(supremePizza);
 recipes.remove(pecanPie);
 recipes.remove(mintChocChipIceCream);
 recipes.remove(extremeChili);
 #recipes.remove(pizza);
 recipes.remove(presser);
 recipes.remove(marzipan);
 recipes.remove(baconMushroomBurger);
 recipes.remove(cheeseBurger);
 recipes.remove(mapleWaffles);
 recipes.remove(cottonSeeds);
 recipes.remove(sausageInBread);
 recipes.remove(baconCheeseBurger);
 recipes.remove(deluxeCheeseBurger);
 recipes.remove(pyrotheum);
 recipes.remove(bloodAltar);
 recipes.remove(MPSmagnet);
 recipes.remove(frostyMint);
 recipes.remove(taffy);
 recipes.remove(peppermint);
 recipes.remove(caramel);
 recipes.remove(piston);
 recipes.remove(yogurt);
 recipes.remove(chiliChoc);
 recipes.remove(hotdog);
 recipes.remove(candiedLemon);
 recipes.remove(blazePowder);
 recipes.remove(sacrificialKnife);
 recipes.remove(<EnderIO:blockIngotStorage:3>);
 recipes.remove(<EnderIO:itemAlloy:3>);
 recipes.remove(<Botania:conjurationCatalyst>);
 recipes.remove(iceCream);
 recipes.remove(chips);
 recipes.remove(flour);
 recipes.removeShaped(cobble, [[littleStone, littleStone], [littleStone, littleStone]]);
 recipes.remove(angelRing);
 recipes.remove(angelRingFeather);
 recipes.remove(angelRingFairy);
 recipes.remove(angelRingDragon);
 recipes.remove(angelRingGold);
 recipes.remove(MPSsolar);
 #NEIhiding
  #Only for whole IDs.
 #Fuels
 furnace.setFuel(apple, 200);
 furnace.setFuel(cookingOil, 1600);
 furnace.setFuel(zeppole, 1600);
 furnace.setFuel(<harvestcraft:walnutItem>, 100);
 furnace.setFuel(<harvestcraft:peanutItem>, 100);
 furnace.setFuel(<harvestcraft:pistachioItem>, 100);
 furnace.setFuel(<harvestcraft:pecanItem>, 100);
 furnace.setFuel(<harvestcraft:cashewItem>, 100);
 furnace.setFuel(marzipan, 600);
 furnace.setFuel(brownie, 1800);
 furnace.setFuel(<harvestcraft:toastItem>, 1200);
 furnace.setFuel(taffy, 2000);
 furnace.setFuel(hotWing, 2400);
 furnace.setFuel(heatBalls, 12000);
 furnace.setFuel(epicBacon, 2560000);
#Oredict entries
 <ore:stickWood>.mirror(<ore:listAllnut>);
 <ore:materialPressedwax>.add(<Forestry:beeswax>);
 <ore:materialPressedwax>.add(<Forestry:refractoryWax>);
 <ore:T1food>.add(<harvestcraft:pearItem>);
 <ore:T1food>.add(<harvestcraft:peachItem>);
 <ore:T1food>.add(<harvestcraft:grapefruitItem>);
 <ore:T1food>.add(<minecraft:apple>);
 <ore:T2food>.mirror(<ore:cropAlmond>);
 <ore:starNether>.add(<minecraft:nether_star>);
 <ore:starNether>.add(epicBacon);
 <ore:ingotMeatRaw>.remove(rawMeatIngot);
 <ore:listAllmeatraw>.add(rawMeatIngot);
 <ore:ingotOsmium>.add(marzipan);
 <ore:foodLemonbar>.remove(marzipan);
 <ore:ingotIron>.add(brownie);
 <ore:ingotSteel>.add(mapleWaffles);
 <ore:ingotGold>.add(butter);
 <ore:ingotAluminum>.add(caramelIceCream);
 <ore:gemDiamond>.add(manjuu);
 <ore:coal>.add(zeppole);
 <ore:circuitElite>.add(MPScircuit);
 <ore:ingotCopper>.add(lavenderBread);
 <ore:listAllmeatraw>.add(rawMeatIngot);
 <ore:cropMint>.add(mint);
 <ore:seedMint>.add(mintSeed);
 <ore:listAllFlourGrains>.add(rice);
 <ore:listAllFlourGrains>.add(potato);
 <ore:listAllFlourGrains>.add(oats);
 <ore:listAllFlourGrains>.add(wheat);
 <ore:listAllFlourGrains>.add(rye);
 <ore:listAllFlourGrains>.add(barley);
 <ore:foodRainbowcurry>.remove(rainbow);
 <ore:foodDelightedmeal>.remove(delightedMeal);
 <ore:snackMix>.add(delightedMeal);
 <ore:foodManjuu>.remove(manjuu);
 <ore:foodLimejelly>.remove(frostyMint);
 <ore:foodLemonbar>.remove(marzipan);
 <ore:cropSeaweed>.remove(mint);
 <ore:seedSeaweed>.remove(mintSeed);
 <ore:foodMarzipan>.add(marzipan);
#Recipes
 #Smelting
  furnace.addRecipe(bread, dough);
  furnace.addRecipe(cheese, yogurt);
 #Shaped Crafting
  recipes.addShaped(<minecraft:planks:0>,[[walnut, walnut], [walnut, walnut]]);
  recipes.addShaped(<minecraft:planks:1>,[[chestnut, chestnut], [chestnut, chestnut]]);
  recipes.addShaped(<minecraft:planks:2>,[[peanut, peanut], [peanut, peanut]]);
  recipes.addShaped(<minecraft:planks:3>,[[cashew, cashew], [cashew, cashew]]);
  recipes.addShaped(<minecraft:planks:4>,[[pecan, pecan], [pecan, pecan]]);
  recipes.addShaped(<minecraft:planks:5>,[[pistachio, pistachio], [pistachio, pistachio]]);
  recipes.addShaped(pureDaisyOut, [[nut, nut, nut], [sugar, apple, sugar], [nut, nut, nut]]);
  recipes.addShaped(redstone, [[null, bloodenCobbleIn.reuse(), null], [goldNugget, red, goldNugget]]);
  recipes.addShaped(extremeChili, [[flintAndSteel.transformDamage(16).onlyDamageAtMost(48), onion , flintAndSteel.transformDamage(16).onlyDamageAtMost(48)], [blazePowder, chili, blazePowder], [null, garlic, null]]);
  recipes.addShaped(sacrificialKnife, [[flint], [stick]]);
  recipes.addShaped(<minecraft:cobblestone> * 4, [[woodPlank, woodPlank, woodPlank], [woodPlank, <ore:T1food>, woodPlank], [woodPlank, woodPlank, woodPlank]]);
  recipes.addShaped(crucible, [[apple, nut, apple], [livingRock, null, livingRock], [livingRock, stone, livingRock]]);
  recipes.addShaped(tileFurnace, [[livingRock, livingRock, livingRock], [livingRock, null, livingRock], [livingRock, livingRock, livingRock]]);
  recipes.addShaped(furnaceGen, [[cobble, tileFurnace, cobble], [redstone, compressedCobble, redstone], [redstone, tileFurnace, redstone]]);
  recipes.addShaped(manaSpreader, [[livingWood, livingWood, livingWood], [monsterJerky, null, redstone], [livingWood, apple, livingWood]]);
  recipes.addShaped(manaSpreader, [[livingWood, livingWood, livingWood], [redstone, null, monsterJerky], [livingWood, apple, livingWood]]);
  recipes.addShaped(bloodenCobbleOut, [[bloodDrop, redstone, bloodDrop], [bloodDrop, tripleCompressedCobble, bloodDrop], [bloodDrop, bloodDrop, bloodDrop]]);
  recipes.addShaped(chest, [[woodPlank, woodPlank, woodPlank], [woodPlank, nut, woodPlank], [woodPlank, woodPlank, woodPlank]]);
  recipes.addShaped(steelSword, [[steel, null, null], [steel, null, null], [iron, null, null]]);
  recipes.addShaped(bloodBrewing, [[null, hotWing, null], [bloodDrop, weakBloodOrb, bloodDrop], [rawRubber, bloodDrop, rawRubber]]);
  recipes.addShaped(brewingStand, [[null, hotWing, null], [cobble, cobble, cobble]]);
  recipes.addShaped(bloodAltar, [[iron, bloodDrop, iron], [livingRock, tileFurnace, livingRock]]);
  recipes.addShaped(metalInfuser, [[iron, tileFurnace, iron], [redstone, osmium, redstone], [iron, tileFurnace, iron]]);
  recipes.addShaped(crusher, [[redstone, mekControlCircuit, redstone], [steel, steelCasing, steel],[redstone,mekControlCircuit, redstone]]);
  recipes.addShaped(piston, [[toastSandwich, jellybeans, toastSandwich], [marzipan, peanutButterCookies, marzipan], [persimmonSandwich, donut, persimmonSandwich]]);
  recipes.addShaped(weakGlowstone, [[MPSsolenoid.reuse(), gold, MPSsolenoid.reuse()], [MPSwiring.reuse(), redstone, MPSwiring.reuse()]]);
  recipes.addShaped(MPShelm, [[MPSironPlating, MPSironPlating, MPSironPlating], [MPSironPlating, hardGlass, MPSironPlating]]);
  recipes.addShaped(MPSchest, [[MPSironPlating, null, MPSironPlating], [MPSironPlating, MPSironPlating, MPSironPlating], [MPSironPlating, MPSdiamondPlating, MPSironPlating]]);
  recipes.addShaped(MPSpants, [[MPSironPlating, MPSironPlating, MPSironPlating], [MPSservo, null, MPSservo], [MPSironPlating, null, MPSironPlating]]);
  recipes.addShaped(MPSboots, [[MPSironPlating, null, MPSironPlating], [MPSservo, null, MPSservo]]);
  recipes.addShaped(MPSfist, [[null, MPSchip, null], [MPSservo, MPSironPlating, null], [null, MPSmuscle, null]]);
  recipes.addShaped(MPSwiring * 6, [[redstone, null, redstone], [copper, redstone, copper], [steel, null, steel]]);
  recipes.addShaped(MPSsolenoid, [[MPSwiring, iron, null], [null, iron, MPSwiring], [MPSwiring, iron, null]]);
  recipes.addShaped(MPSservo, [[osmium, MPSwiring, osmium], [MPSwiring, MPSsolenoid, MPSwiring]]);
  recipes.addShaped(MPSwing, [[hotWing, MPSwiring, aluminium], [MPSwiring, diamond, null], [aluminium, null, null]]);
  recipes.addShaped(MPSthruster, [[bucketBlue, MPSmagnet, accelerationCard], [enderiumBlock, MPSffEmitter, diamondBlock], [bucketYellow, MPSmagnet, accelerationCard]]);
  recipes.addShaped(MPSlv, [[MPSwiring, taffy, MPSsolenoid]]);
  recipes.addShaped(MPSmv, [[MPSwiring, <Mekanism:EnergyTablet:1>.withTag({electricity: 1000000.0}), MPSwiring]]);
  recipes.addShaped(MPShv, [[MPSsolenoid, <Mekanism:EnergyTablet:1>.withTag({electricity: 1000000.0}).transformReplace(<Mekanism:EnergyTablet:100>.withTag({electricity: 0.0})), MPSsolenoid], [eliteCircuit, eliteECube, eliteCircuit]]);
  recipes.addShaped(MPSparachute, [[wool, wool, wool], [itemString, nut, itemString]]);
  recipes.addShaped(MPSironPlating, [[iron, iron], [steel, MPSwiring], [iron, iron]]);
  recipes.addShaped(MPSdiamondPlating, [[diamond, diamond], [diamond, steelBall], [diamond, diamond]]);
  recipes.addShaped(MPSffEmitter, [[enderium, MPSholoEmitter, enderium], [MPSsolenoid, fullTesseFrame, MPSsolenoid], [enderium, diamond, enderium]]);
  recipes.addShaped(MPSholoEmitter, [[glowstoneBlock, glowstone, glowstoneBlock], [MPSsolenoid, diamond, MPSsolenoid], [redIllumar, greenIllumar, blueIllumar]]);
  recipes.addShaped(MPSgel, [[jellybeans], [gold], [jellybeans]]);
  recipes.addShaped(MPSmyofiber * 3, [[diamond], [compressedCarbon], [diamond]]);
  recipes.addShaped(MPSmuscle, [[MPSmyofiber, MPSgel, MPSmyofiber], [MPSmyofiber, MPSsolenoid, MPSmyofiber], [MPSmyofiber, MPSgel, MPSmyofiber]]);
   #Shapeless Crafting
  recipes.addShapeless(bloodDrop, [nut, chicken, bonemeal, cookingOil]);
  recipes.addShapeless(gravel, [cobble, nut, nut, nut]);
  recipes.addShapeless(butter, [goldNugget, brownie, milkBucket, skillet]);
  recipes.addShapeless(flour, [<ore:listAllFlourGrains>, <ore:listAllFlourGrains>, mortarAndPestle]);
  recipes.addShapeless(manjuu, [butter, grilledCheese, taffy, aluminium, pink, coal, manaSteelBlock, redstone]);
  recipes.addShapeless(pyrotheum * 2, [extremeChili, coalDust, extremeChili, redstone]);
  recipes.addShapeless(mapleWaffles, [steel]);
  recipes.addShapeless(dandelion, [yellow]);  
  recipes.addShapeless(dandelion, [peanutButter, bonemeal]);
  recipes.addShapeless(rawRubber * 5, [taffy, taffy]);
  recipes.addShapeless(HChoney, [taffy, yellow]);
  recipes.addShapeless(frostedDonut, [donut, sugar, fruitSalad, taffy]);
  recipes.addShapeless(taffy, [waterBucket, sugar, sugar, sugar, saucepan, flintAndSteel.transformDamage(2).onlyDamageAtMost(62)]);
  recipes.addShapeless(vanillaCrop, [stoneBricks, lever, craftingTable, chest]);
  recipes.addShapeless(zeppole, [potato, saucepan, <harvestcraft:peanutItem>, <ore:listAllnut>, flour]);
  recipes.addShapeless(bonemeal * 3, [<minecraft:bone>, nut, nut, mortarAndPestle]);
  recipes.addShapeless(<minecraft:coal>, [zeppole]);
  recipes.addShapeless(<minecraft:coal:1>, [cookingOil]);
  recipes.addShapeless(salt, [waterBucket, pot, nut]);  
  recipes.addShapeless(marzipan, [almond, mortarAndPestle, pot, sugar]);
  recipes.addShapeless(yogurt, [pot, salt, <ore:listAllmilk>, sugar]);
  recipes.addShapeless(hotWing, [gunpowder, chicken, chili, flintAndSteel.transformDamage(4).onlyDamageAtMost(60)]);
  recipes.addShapeless(blazePowder, [hotWing, chili, pot, saucepan, flintAndSteel.transformDamage(8).onlyDamageAtMost(56)]);
  recipes.addShapeless(wheatSeeds, [bucketRed, nut, wheat]);
  recipes.addShapeless(rawMeatBlock, [<ore:listAllmeatraw>, <ore:listAllmeatraw>, <ore:listAllmeatraw>, <ore:listAllmeatraw>, <ore:listAllmeatraw>, <ore:listAllmeatraw>, <ore:listAllmeatraw>, <ore:listAllmeatraw>, <ore:listAllmeatraw>]);
  recipes.addShapeless(brownie, [bakeware, walnut, cocoaPowder, flour, <minecraft:egg>, vanilla, sugar, woodPlank]); 
  recipes.addShapeless(cinnamonSapling, [appleSapling, pistachioSapling, cashewSapling, bloodDrop]);
  recipes.addShapeless(<harvestcraft:walnutItem>, [peanut, peanut, apple, bloodenCobbleIn.reuse()]);
  recipes.addShapeless(<harvestcraft:pecanItem>, [walnut, bloodenCobbleIn.reuse()]);
  recipes.addShapeless(<harvestcraft:pistachioItem>, [pecan, bloodenCobbleIn.reuse()]);
  recipes.addShapeless(<harvestcraft:cashewItem>, [pistachio, bloodenCobbleIn.reuse()]);
  recipes.addShapeless(<harvestcraft:chestnutItem>, [cashew, bloodenCobbleIn.reuse()]);
  recipes.addShapeless(<harvestcraft:walnutItem>, [chestnut, bloodenCobbleIn.reuse()]);
  recipes.addShapeless(deluxeCheeseBurger, [baconCheeseBurger, lettuce, vegetable, tomato, chips]);
  recipes.addShapeless(epicBacon, [rareBacon, baconDates, mapleBacon, rainbow, fairySecret, candySecret, pigPlate, <minecraft:cooked_porkchop>, pigIngot]);
  recipes.addShapeless(rainbow, [bucketRed, bucketOrnage, bucketYellow, fruitSalad, bucketGreen, bucketBlue, bucketIndigo, bucketPurple]);
 
 #Botania
  #Rune altar
   mods.botania.RuneAltar.addRecipe(skyStone, [stone, goldenApple], 50);
   mods.botania.RuneAltar.addRecipe(cocoa, [<minecraft:planks:3>, <minecraft:planks:5>, cashew, pistachio], 200);
   mods.botania.RuneAltar.addRecipe(redstone, [livingRock, livingRock, livingRock, apple, apple, apple, <minecraft:planks:5>], 200);
  #Mana Removal
   mods.botania.ManaInfusion.removeRecipe(<minecraft:leather>);
   mods.botania.ManaInfusion.removeRecipe(<minecraft:glowstone_dust>);
  #Mana lvl 1
   mods.botania.ManaInfusion.addInfusion(livingWood, <minecraft:planks:5>, 500);
   mods.botania.ManaInfusion.addInfusion(spawnChicken, chicken, 5000);
   mods.botania.ManaInfusion.addInfusion(spawnCow, steak, 10000);
   mods.botania.ManaInfusion.addInfusion(goldNugget, hotWing, 1000);
   mods.botania.ManaInfusion.addInfusion(chili, chiliChoc, -1000);
  #Mana lvl 2
   mods.botania.ManaInfusion.addAlchemy(netherrack, extremeChili, 1000);
   mods.botania.ManaInfusion.addAlchemy(chargedCertus, certus, 1500);
  #Orechid
   mods.botania.Orechid.removeOre(<ore:oreIron>);
   mods.botania.Orechid.removeOre(<ore:oreDiamond>);
   mods.botania.Orechid.removeOre(<ore:oreGold>);
   mods.botania.Orechid.removeOre(<ore:oreCoal>);
   mods.botania.Orechid.removeOre(<ore:oreRedstone>);
   mods.botania.Orechid.removeOre(<ore:oreCopper>);
   mods.botania.Orechid.removeOre(<ore:oreSilver>);
   mods.botania.Orechid.removeOre(<ore:oreLead>);
   mods.botania.Orechid.removeOre(<ore:oreNickel>);
   mods.botania.Orechid.removeOre(<ore:oreTin>);
 #Tinker's Construct
  #Drying Rack
   mods.tconstruct.Drying.addRecipe(mint, teaLeaf, 100);
  #Stats changes
  #mods.tconstruct.ToolStats.setDisplayName("Wood","Nut ");
  #Smeltery melting
   mods.tconstruct.Smeltery.addMelting(<ore:listAllmeatraw>, <liquid:meat> * 25, 80, rawMeatBlock);
   mods.tconstruct.Smeltery.addMelting(mintBalls, <liquid:ice> * 100, 40, iceBlock);
  #Smeltery alloys
   mods.tconstruct.Smeltery.addAlloy(<liquid:mana> * 1000, [<liquid:xpjuice> * 250, <liquid:ender> * 250, <liquid:liquiddna> * 5]);
   mods.tconstruct.Smeltery.addAlloy(<liquid:mana> * 250, [<liquid:xpjuice> * 250, <liquid:ender> * 250]);
  #Casting
   mods.tconstruct.Casting.addTableRecipe(cookedPork, <liquid:meat> * 20, rawPork, true, 20);
   mods.tconstruct.Casting.addTableRecipe(steak, <liquid:meat> * 20, beef, true, 20);
   mods.tconstruct.Casting.addTableRecipe(cookedChicken, <liquid:meat> * 20, chicken, true, 20);
   mods.tconstruct.Casting.addTableRecipe(cookedMutton, <liquid:meat> * 20, rawMutton, true, 20);
   mods.tconstruct.Casting.addTableRecipe(cookedSalmon, <liquid:meat> * 20, rawSalmon, true, 20);
   mods.tconstruct.Casting.addTableRecipe(cookedFish, <liquid:meat> * 20, rawFish, true, 20);
   mods.tconstruct.Casting.addTableRecipe(yellorium, <liquid:sludge> * 1000, refGlowstone, true, 100);
   mods.tconstruct.Casting.addTableRecipe(<minecraft:glowstone_dust>, <liquid:redstone> * 100, weakGlowstone, true, 20);
   mods.tconstruct.Casting.addTableRecipe(<minecraft:blaze_rod>, <liquid:pyrotheum> * 200, hotWing, true, 20);
   mods.tconstruct.Casting.addTableRecipe(netherQuartz, <liquid:obsidian.molten> * 288, caramel, true, 40);
   mods.tconstruct.Casting.addTableRecipe(certus, <liquid:silver.molten> * 288, caramel, true, 40);
 #Thermal Expansion
  #Fluid transposer
   mods.thermalexpansion.Transposer.addRecipe(1000, bread, hotdog, <liquid:meat> * 100, 100, true);
   mods.thermalexpansion.Transposer.addRecipe(10000, hotWing, boneItem, <liquid:pyrotheum> * 20, 100, false);
   mods.thermalexpansion.Transposer.addRecipe(1000, chocolateBar, mintChocChipIceCream, <liquid:ice> * 100, 100, true);
   mods.thermalexpansion.Transposer.addRecipe(1200, mintBalls, iceCream, <liquid:milk> * 1000, 100, true);
   mods.thermalexpansion.Transposer.addRecipe(4000, <harvestcraft:walnutItem>, cookingOil, <liquid:seedoil> * 60, 100, true);
  #Induction Smelter
   mods.thermalexpansion.Smelter.addRecipe(400, lemon, sugar, candiedLemon);
   mods.thermalexpansion.Smelter.addRecipe(400, fruitSalad, sugar * 3, jellybeans);
   mods.thermalexpansion.Smelter.addRecipe(800, rawPork, date, baconDates);
   mods.thermalexpansion.Smelter.addRecipe(8000, beef, bread, hamburger);
   mods.thermalexpansion.Smelter.addRecipe(8000, salt, potato, chips);
   mods.thermalexpansion.Smelter.addRecipe(16000, frostyMint, extremeChili, obsidian * 2);
 #Blood Magic
  #Blood altar
  Altar.removeRecipe(weakBloodOrb);
  Altar.addRecipe(leather, rottenFlesh, 1, 200);
  Altar.addRecipe(spawnCow, spawnChicken, 1, 2600);
  Altar.addRecipe(weakBloodOrb, compressedCobble, 1, 2000);
  #Chemistry
   Alchemy.removeRecipe(bread);
   Alchemy.addRecipe(sugar, [sugarcane, sugarcane, apple], 1, 200);
   Alchemy.addRecipe(chiliChoc, [chocolateBar, chili, pyrotheum, redPetal], 2, 2000);
 #Buildcraft
  #Assembly Table
   mods.buildcraft.AssemblyTable.addRecipe(caramel, 4000, [sugar * 8]);
   mods.buildcraft.AssemblyTable.addRecipe(butter, 6000, [freshMilk * 2, brownie * 2]);
   mods.buildcraft.AssemblyTable.addRecipe(heatBalls, 8000, [pyrotheum, baconDates, dough, hotWing]);
   mods.buildcraft.AssemblyTable.addRecipe(oilCapsule, 80000, [<harvestcraft:peanutItem> * 32, searedBrick * 16, cookingOil]);
 #Mekanism
  #Metallurgic infuser
   mods.mekanism.Infuser.addRecipe("OBSIDIAN", 20, frostyMint, MPScircuit);
  #Crusher
   mods.mekanism.Crusher.addRecipe(dandelion, yellow * 2);
  #Enrichment Chamber
   mods.mekanism.Enrichment.addRecipe(mintBalls * 4, frostyMint);
   mods.mekanism.Enrichment.addRecipe(wheat, flour);
   mods.mekanism.Enrichment.addRecipe(rice, flour);
   mods.mekanism.Enrichment.addRecipe(potato, flour);
   mods.mekanism.Enrichment.addRecipe(barley, flour);
   mods.mekanism.Enrichment.addRecipe(rye, flour);
   mods.mekanism.Enrichment.addRecipe(oats, flour);
   mods.mekanism.Enrichment.addRecipe(rawMutton, cotton * 2);
  #Chemical Injection
   mods.mekanism.chemical.Injection.removeRecipe(clayBlock);
  #Purification
   mods.mekanism.Purification.addRecipe(mint * 6, mintBalls * 4);
  #Combiner
 #Ex Nihilo
  Crucible.addRecipe(rawMeatBlock, <liquid:meat> * 200);
  Crucible.addRecipe(redstoneBlock, <liquid:redstone> * 800);
  Crucible.removeHeatSource(<minecraft:fire>);
  Crucible.addHeatSource(<minecraft:fire>, 1.0);
